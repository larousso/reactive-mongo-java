buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
    id "maven"
    id "maven-publish"
    id "java"
    id 'ch.netzwerg.release' version '1.2.5'
}

group 'com.adelegue'

jar {
    baseName = 'reactive-mongo-java'
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

def akkaVersion = "2.5.14"
def scalaVersion = "2.12"

dependencies {
    compile("io.vavr:vavr:0.9")
    compile("com.typesafe.akka:akka-stream_${scalaVersion}:${akkaVersion}")
    compile("org.reactivecouchbase.json:json-lib:1.0.0")
    compile("io.javaslang:javaslang-jackson:2.0.5")
    compile("org.mongodb:mongodb-driver-reactivestreams:1.9.0")
    testCompile("com.typesafe.akka:akka-testkit_2.11:2.4.16")
    testCompile("com.github.fakemongo:fongo:2.0.6")
    testCompile("org.slf4j:slf4j-api:1.7.22")
    testCompile("org.assertj:assertj-core:3.6.2")
    testCompile("junit:junit:4.12")
    testCompile("org.assertj:assertj-core:3.5.2")
    testCompile("de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_PASSWORD')
    publications = ['mavenPublication']
    pkg {
        name = 'reactive-mongo-java'
        repo = 'maven'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/larousso/reactive-mongo-java.git'
        publish = true
        version {
            name = "${project.version}"
        }
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'com.adelegue'
            artifactId 'reactive-mongo-java'
            version "${project.version}"
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

release {
    dependsOn build
    push = true
    versionSuffix = '-SNAPSHOT' // '.DEV' or '' (empty) could be useful alternatives
    tagPrefix = 'v' // 'r' or '' (empty) could be useful alternatives
}